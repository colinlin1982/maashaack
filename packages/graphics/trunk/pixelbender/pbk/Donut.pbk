/*
  Version: MPL 1.1/GPL 2.0/LGPL 2.1
 
  The contents of this file are subject to the Mozilla Public License Version
  1.1 (the "License"); you may not use this file except in compliance with
  the License. You may obtain a copy of the License at
  http://www.mozilla.org/MPL/
  
  Software distributed under the License is distributed on an "AS IS" basis,
  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
  for the specific language governing rights and limitations under the
  License.
  
  The Original Code is [maashaack framework].
  
  The Initial Developers of the Original Code are
  Zwetan Kjukov <zwetan@gmail.com> and Marc Alcaraz <ekameleon@gmail.com>.
  Portions created by the Initial Developers are Copyright (C) 2006-2013
  the Initial Developers. All Rights Reserved.
  
  Contributor(s):
  
  Alternatively, the contents of this file may be used under the terms of
  either the GNU General Public License Version 2 or later (the "GPL"), or
  the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
  in which case the provisions of the GPL or the LGPL are applicable instead
  of those above. If you wish to allow use of your version of this file only
  under the terms of either the GPL or the LGPL, and not to allow others to
  use your version of this file under the terms of the MPL, indicate your
  decision by deleting the provisions above and replace them with the notice
  and other provisions required by the LGPL or the GPL. If you do not delete
  the provisions above, a recipient may use your version of this file under
  the terms of any one of the MPL, the GPL or the LGPL.
*/

<languageVersion : 1.0;>

kernel Donut
<   
    namespace   : "graphics.filters";
    vendor      : "ALCARAZ Marc (eKameleon)";
    version     : 1;
    description : "Pixelate an input image and tile it with donut like shapes." ; 
>

{
    parameter float count
    <
        minValue: 1.0;
        maxValue: 100.0;
        defaultValue: 5.0;
    >;

    parameter float min
    <
        minValue: 0.0;
        maxValue: 1.0;
        defaultValue: 0.25;
    >;

    parameter float max
    <
        minValue: 0.0;
        maxValue: 1.0;
        defaultValue: 0.45;
    >;

    parameter float width
    <
        minValue: 1.0;
        maxValue: 1000.0;
        defaultValue: 100.0;
    >;

    parameter float height
    <
        minValue: 1.0;
        maxValue: 1000.0;
        defaultValue: 100.0;
    >;

    parameter pixel4 color
    <
        minValue     : float4( 0.0 , 0.0 , 0.0 , 0.0 );
        maxValue     : float4( 1.0 , 1.0 , 1.0 , 1.0 );
        defaultValue : float4( 0.2 , 0.2 , 0.2 , 1.0 );
    >;

    input image4 source ;

    output pixel4 result ;

    void evaluatePixel()
    {
        float2 pixel = outCoord();
        
        float num = count / 5.0 ;
        
        float size = 1.0 / num ;
                
        pixel.x = pixel.x / width  ;
        pixel.y = pixel.y / height ;

           float2 position = floor( pixel * num ) ;
           float2 center   = position * size + size * 0.5;
        float  distance = length( pixel - center ) * num ;

           if( distance < min || distance > max )
           {
            result = color ;
           }
        else
        {
            center.x = center.x * width  ;
            center.y = center.y * height ;
            
            result = sampleNearest( source , center );
        }
    }
}